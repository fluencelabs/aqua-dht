module Main

import App from "deployed.app.aqua"
import EchoService from "services/echoService.aqua"
import "@fluencelabs/registry/resources-api.aqua"
export App, echo, echoJS, registerResource, registerServiceRecord, echoAll

func echo(msg: string) -> *string:
    services <- App.services()
    results: *string

    for srv <- services.echoService.default:
        on srv.peerId:
            EchoService srv.serviceId
            results <- EchoService.echo(msg)
    <- results

func echoJS(peer: string, relay: string, serviceId: string, msg: string) -> string:
    on peer via relay:
        EchoService serviceId
        res <- EchoService.echo(msg)
    <- res

func registerResource() -> ?string:
    resource_id, error <- createResource("echo")
    <- resource_id

func registerService(resource_id: string) -> *bool:
    results: *bool
    services <- App.services()
    for srv <- services.echoService.default:
        results, error <- registerServiceRecord(srv.peerId, resource_id, "", ?[srv.serviceId])
    <- results

func echoAll(resource_id: string, msg: string) -> *string:
    -- 2 is the min number of providers we want to find
    records <- resolveResource(resource_id, 2)
    results: *string
    for r <- records:
        on r.peer_id via r.relay_id:
            EchoService r.service_id!
            results <- EchoService.echo(msg)
    <- results